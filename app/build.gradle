plugins {
    id "com.android.application"
    id "com.github.triplet.play"
    id "com.google.firebase.crashlytics"
    id "kotlin-android"
    id "kotlin-android-extensions"
    id "kotlin-kapt"
}

apply from: "$project.rootDir/scripts/credentials.gradle"
apply plugin: 'kotlin-android'
apply from: "$project.rootDir/scripts/ktlint.gradle"
apply from: "$project.rootDir/scripts/signing.gradle"

android {
    compileSdkVersion 31
    ndkVersion "21.3.6528147"

    defaultConfig {
        applicationId "kz.sozdik.free"
        minSdkVersion 21
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 20201115
        versionName "3.5"
        multiDexEnabled true
        signingConfig signingConfigs.release
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // TODO: Remove the code below https://github.com/moxy-community/Moxy/wiki/Migration-Guide-(1.x.x-to-2.x.x)
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        disableEmptyStrategyCheck: "true",
                ]
            }
        }
        resConfigs 'en', 'kk', 'ky', 'ru', 'ar'
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            debuggable true

            buildConfigField "String", "SOZDIK_API_CLIENT_NAME", "\"$SOZDIK_API_CLIENT_NAME\""
            buildConfigField "String", "SOZDIK_API_KEY", "\"$SOZDIK_API_KEY\""
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            buildConfigField "String", "SOZDIK_API_CLIENT_NAME", "\"$SOZDIK_API_CLIENT_NAME\""
            buildConfigField "String", "SOZDIK_API_KEY", "\"$SOZDIK_API_KEY\""
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}
play {
    serviceAccountCredentials = file("google-credentials.json")
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.browser:browser:1.4.0"

    implementation "com.google.android.material:material:1.5.0"
    implementation "com.google.android.gms:play-services-auth:20.0.1"

    implementation platform('com.google.firebase:firebase-bom:26.3.0')
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-auth"
    implementation "com.google.firebase:firebase-config"
    implementation "com.google.firebase:firebase-core"
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-messaging"

    implementation "com.facebook.android:facebook-login:5.9.0"

    def coroutinesVersion = '1.3.9'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    // https://github.com/google/dagger
    def daggerVersion = "2.28.3"
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // https://github.com/Arello-Mobile/Moxy
    def moxyVersion = "2.2.2"
    implementation "com.github.moxy-community:moxy:$moxyVersion"
    implementation "com.github.moxy-community:moxy-androidx:$moxyVersion"
    kapt "com.github.moxy-community:moxy-compiler:$moxyVersion"

    // https://square.github.io/retrofit/
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // https://github.com/google/gson
    implementation "com.google.code.gson:gson:2.8.6"

    implementation "org.jsoup:jsoup:1.13.1"

    // https://github.com/facebook/fresco
    def frescoVersion = "2.0.0"
    implementation "com.facebook.fresco:fresco:$frescoVersion"
    implementation "com.facebook.fresco:animated-gif:$frescoVersion"

    def roomVersion = "2.4.1"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // https://github.com/akexorcist/Android-LocalizationActivity
    implementation "com.akexorcist:localizationactivity:1.2.2"
    // https://github.com/Pixplicity/EasyPrefs
    implementation "com.pixplicity.easyprefs:EasyPrefs:1.10.0"

    // https://github.com/zhpanvip/viewpagerindicator
    implementation 'com.github.zhpanvip:viewpagerindicator:1.2.1'
    // https://github.com/booncol/Pulsator4Droid
    implementation "pl.bclogic:pulsator4droid:1.0.3"

    implementation "com.github.askarsyzdykov:bubbles-for-android:1.2.1.8"

    // https://github.com/square/okhttp
    def okHttpVersion = "4.8.1"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    debugImplementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    def stethoVersion = "1.5.1"
    debugImplementation("com.facebook.stetho:stetho:$stethoVersion") {
        exclude group: "com.google.code.findbugs", module: "jsr305"
    }
    debugImplementation("com.facebook.stetho:stetho-okhttp3:$stethoVersion") {
        exclude group: "com.google.code.findbugs", module: "jsr305"
    }

    // https://github.com/JakeWharton/timber
    implementation "com.jakewharton.timber:timber:4.7.1"

    // https://square.github.io/leakcanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.4"

    // https://github.com/KasperskyLab/Kaspresso
    androidTestImplementation "com.kaspersky.android-components:kaspresso:1.4.0"

    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:2.2.0"

    def espressoVersion = "3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
}

apply plugin: "com.google.gms.google-services"
